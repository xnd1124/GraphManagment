
#Область СлужебныеПроцедурыИФункции
&НаСервереБезКонтекста
&ИзменениеИКонтроль("ИнициализироватьКомпоновщикНастроек")
Процедура граф_ИнициализироватьКомпоновщикНастроек(КомпоновщикНастроек, АдресСхемыКомпоновкиДанных, АдресНастроекКомпоновкиДанных, ПроцедураПриИнициализацииНастроекКомпоновки)

	СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);
	Если ЗначениеЗаполнено(АдресНастроекКомпоновкиДанных) Тогда
		НастройкиКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресНастроекКомпоновкиДанных);
	КонецЕсли;

	Попытка
		КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных));
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Выполнение операции';
		|en = 'Performing operation'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Ошибка,,,
		ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	#Вставка
	СтарыеПараметрыДанных = КомпоновщикНастроек.Настройки.ПараметрыДанных;	
	#КонецВставки
	
	Если ЗначениеЗаполнено(АдресНастроекКомпоновкиДанных) Тогда
		КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиКомпоновкиДанных);
	Иначе
		КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	КонецЕсли;  
	
	#Вставка
	ЗаполненныеПараметры = Новый Соответствие;
	Для каждого Значение Из СтарыеПараметрыДанных.Элементы Цикл
		Если Значение.Использование = Ложь Тогда
			Продолжить;
		КонецЕсли;     
		ЗаполненныеПараметры.Вставить(Значение.Параметр , Значение.Значение);
	КонецЦикла; 
	
	НовыеЭлементы = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы;
	Для каждого Значение Из НовыеЭлементы Цикл  
		СохраненноеЗначение = ЗаполненныеПараметры.Получить(Значение.Параметр);
		Если СохраненноеЗначение = Неопределено Тогда
			Продолжить;
		КонецЕсли;     
		Значение.Использование = Истина;
		Значение.Значение = СохраненноеЗначение;
	КонецЦикла; 
	#КонецВставки

	Если НЕ ПустаяСтрока(ПроцедураПриИнициализацииНастроекКомпоновки) Тогда
		ВыполнитьМетодПриИнициализацииНастроекКомпоновки(ПроцедураПриИнициализацииНастроекКомпоновки,
		СхемаКомпоновкиДанных,
		КомпоновщикНастроек);
	КонецЕсли;

	КомпоновщикНастроек.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);

КонецПроцедуры
#КонецОбласти
