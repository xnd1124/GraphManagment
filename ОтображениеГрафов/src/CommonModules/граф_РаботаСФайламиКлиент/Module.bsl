#Область ПрограммныйИнтерфейс
// Проверяет наличие библиотеки в файловой системе и устанавливает ее при отсутствии.
//
// Параметры:
//   ИмяБиблиотеки - Строка - Имя библиотеки для проверки и установки
Процедура ПроверитьИУстановитьБиблиотеку(ИмяБиблиотеки) Экспорт
	ПутьКБиблиотеке = ПутьКБиблиотеке(ИмяБиблиотеки);
	Файл = Новый Файл(ПутьКБиблиотеке); 
    Если Файл.Существует() Тогда
    	Возврат;
    КонецЕсли;
    ТекстовыйФайл = Новый ТекстовыйДокумент;
    ИмяМакета = "граф_Библиотека" + ИмяБиблиотеки;
    ТекстБиблиотеки = граф_МакетыВызовСервера.ТекстМакета(ИмяМакета);
    ТекстовыйФайл.УстановитьТекст(ТекстБиблиотеки);
	ТекстовыйФайл.Записать(ПутьКБиблиотеке, КодировкаТекста.UTF8);
КонецПроцедуры

// Формирует полный путь к файлу библиотеки во временном каталоге клиента.
//
// Параметры:
//   ИмяБиблиотеки - Строка - Имя файла библиотеки без расширения.
//
// Возвращаемое значение:
//   Строка - Полный путь к файлу библиотеки.
Функция ПутьКБиблиотеке(ИмяБиблиотеки) Экспорт
	РазделительПути = ПолучитьРазделительПутиКлиента();
	ШаблонПути =  "%1%2%3.js";
	Возврат СтрШаблон(ШаблонПути, КаталогВременныхФайлов(), РазделительПути, ИмяБиблиотеки);
КонецФункции

// Создает текстовый файл по указанному пути с заданным содержимым.
//
// Параметры:
//   ПолныйПуть - Строка - Полный путь к создаваемому файлу.
//   Содержимое - Строка - Текст, который будет записан в файл.
Процедура СоздатьТекстовыйФайл(ПолныйПуть, Содержимое) Экспорт
	ТекстовыйФайл = Новый ТекстовыйДокумент;
    ТекстовыйФайл.УстановитьТекст(Содержимое);
    ТекстовыйФайл.Записать(ПолныйПуть, КодировкаТекста.UTF8);
КонецПроцедуры

#КонецОбласти

// 

