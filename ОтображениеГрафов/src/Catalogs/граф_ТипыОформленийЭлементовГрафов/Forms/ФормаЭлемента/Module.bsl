
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗакешироватьДанныеССервера();
	НастроитьВнешнийВидФормы();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	граф_БиблиотекаG6Клиент.ПроверитьиУстановитьБиблиотеку();
	СформироватьКодДляПросмотраЭлемента();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура КодПриИзменении(Элемент)
	Если Не ЗначениеЗаполнено(Объект.Наименование) Тогда
		Объект.Наименование = Объект.Код;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТипЭлементаПриИзменении(Элемент)
	ТипЭлементаПриИзмененииНаСервере();
	СформироватьКодДляПросмотраЭлемента();
КонецПроцедуры


&НаКлиенте
Процедура ОписаниеЭлементаJSПриИзменении(Элемент)
	СформироватьКодДляПросмотраЭлемента();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ЗакешироватьДанныеССервера()
	КешированныйШаблонТекстаСтраницы = граф_МакетыВызовСервера.ТекстМакета("граф_ШаблонHTML");
	КешированныйСкриптИнициализации = граф_МакетыВызовСервера.ТекстМакета("граф_СкриптИнициализацииГрафа");
	КешированныйТекстСоставляющего = Перечисления.граф_КомпонентыГрафа.ОписаниеКомпонентаГрафа(Объект.Компонент);
КонецПроцедуры

&НаСервере
Процедура НастроитьВнешнийВидФормы()
	Если Объект.Предопределенный Тогда
		ТолькоПросмотр = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СформироватьКодДляПросмотраЭлемента()
		
	ШаблонТекстаСтраницы = СтрЗаменить(КешированныйШаблонТекстаСтраницы, "//инициализация графа", КешированныйСкриптИнициализации);
	
	ШаблонТекстаСтраницы = СтрЗаменить(ШаблонТекстаСтраницы, "//данные графа", КешированныйТекстСоставляющего);
	
	Если ЗначениеЗаполнено(Объект.ОписаниеJS) Тогда
		ШаблонТекстаСтраницы = СтрЗаменить(ШаблонТекстаСтраницы, "// регистрация типа элемента", Объект.ОписаниеJS);
	КонецЕсли;
	ШаблонТекстаСтраницы = СтрЗаменить(ШаблонТекстаСтраницы, "//тип объекта", Объект.Код);
	
	БиблиотекаG6 = граф_БиблиотекаG6Клиент.ТекстПодключенияВHTML();
	ШаблонТекстаСтраницы = СтрЗаменить(ШаблонТекстаСтраницы,
			"// библиотека G6",	БиблиотекаG6);	
		
	ВнешнийВид = ШаблонТекстаСтраницы;
	
КонецПроцедуры

&НаСервере
Процедура ТипЭлементаПриИзмененииНаСервере()
	КешированныйТекстСоставляющего = Перечисления.граф_КомпонентыГрафа.ОписаниеКомпонентаГрафа(Объект.Компонент);
КонецПроцедуры

#КонецОбласти

